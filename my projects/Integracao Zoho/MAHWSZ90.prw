#Include "TOTVS.CH"
#Include "RESTFUL.CH"
/*


Ŀ
Funcao     MAHOPERA  Autor  Ednei R. Silva         Data  MAR/2020 
Ĵ
Descricao  Servico Web service para integracao Protheus X Zoho.       
           Tipo de Operacao.                                          
Ĵ
Sintaxe                                                               
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno                                                               
Ĵ
 Uso       Especifico MA Hospitalar                                   
Ĵ
                          ULTIMAS ALTERACOES                           
Ĵ
Programador  Data    Motivo da Alteracao                             
Ĵ
															              
ٱ

*/

WSRESTFUL MAHOPERA DESCRIPTION "Tipo de Operacao"

	WSDATA DA3_COD AS STRING
	WSDATA DA3_PLACA AS STRING
	WSDATA DA3_DTHDE AS STRING 
	WSDATA DA3_DTHATE AS STRING 
	
	
	WSMETHOD GET DESCRIPTION "Listar Tipo de operacao" WSSYNTAX "/MAHOPERA" 
 
END WSRESTFUL


/*


ͻ
Programa   GET       Autor  Ednei R. Silva         Data  MAR/2020 
͹
Desc.      Metodo para listar Tipo de operacao       Protheus.        
͹
Uso                                                                   
ͼ


*/
WSMETHOD GET WSRECEIVE DA3_COD,DA3_PLACA,DA3_DTHDE,DA3_DTHATE  WSSERVICE MAHOPERA

	Local cQuery	:= ""
	Local cArea		:= ""
	Local aCampo	:= {}
	Local nX		:= 0
	Local nCount	:= 0
	Local nReg		:= 0
	Local nCampo	:= 0
	Local lOk		:= .T.	
	 
	 
	// Define o tipo de retorno do metodo
	::SetContentType("application/json")
	
	aAdd(aCampo, {"X5_CHAVE", "COD"})	
	aAdd(aCampo, {"X5_DESCRI", "DESCRI"})
	aAdd(aCampo, {"D_E_L_E_T_", "DELET"})

	nCampo := Len(aCampo)
		
	cQuery := " SELECT "
	aEval( aCampo, { |Z| nX++, cQuery += Z[1] +" "+ Z[2] + IIf( nX < nCampo, ', ', '') } )
	cQuery += "	FROM " + RetSQLName("SX5")
	cQuery += " WHERE X5_TABELA = 'DJ'  "
	cQuery += "   AND D_E_L_E_T_<>'*' " // Somente Ativo
	cQuery += " AND X5_CHAVE IN ('01','05','06','08','09','10','11','14','15','21','18','22','60','61','62','63','64') "
	cQuery := ChangeQuery(cQuery)			
	
	dbUseArea( .T., "TOPCONN", TcGenQry( ,,cQuery), (cArea := GetNextAlias()), .F., .T. )
	
	Count To nReg		

	If nReg == 0
	
		lOk := .F.
		SetRestFault( 1, "Nenhuma Operacao a ser exibida." )

	ELse
	
		dbSelectArea(cArea)
		dbGoTop()

		::SetResponse('{"OPERACAO": [')

		While (cArea)->( !Eof() )
		
			nCount++

			::SetResponse('{')
			
			nX := 0
			For nX := 1 To nCampo
			
				::SetResponse('"')
				::SetResponse(aCampo[nX][1]) 
				::SetResponse('"')
				::SetResponse(':')
				::SetResponse('"')
				
				If aCampo[nX][1] == "X5_DESCRI"
					::SetResponse( U_NoCharEsp( AllTrim( cValToChar( (cArea)->&(aCampo[nX][2]) ) ) ) )
				Else
					::SetResponse( AllTrim( cValToChar( (cArea)->&(aCampo[nX][2]) ) ) )
				EndIf 
				
				::SetResponse('"')				
				
				If nX < nCampo 
					::SetResponse(',')
				EndIf
							
			Next nX
			
			::SetResponse('}')

			If nCount < nReg
			 	::SetResponse(',')
			EndIf	
	
			(cArea)->( dbSkip() )
	  			
		EndDo
	
		::SetResponse(']}')
		
		(cArea)->( dbCloseArea() )
		
	EndIf
		
Return( lOk )
