#Include "TOTVS.CH"
#Include "RESTFUL.CH"
/*


Ŀ
Funcao     MAHOPERA  Autor  Ednei R. Silva         Data  MAR/2020 
Ĵ
Descricao  Servico Web service para integracao Protheus X Zoho.       
           Tipo.                                                      
Ĵ
Sintaxe                                                               
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno                                                               
Ĵ
 Uso       Especifico MA Hospitalar                                   
Ĵ
                          ULTIMAS ALTERACOES                           
Ĵ
Programador  Data    Motivo da Alteracao                             
Ĵ
															              
ٱ

*/

WSRESTFUL MAHCLI3 DESCRIPTION "Clientes em terceiros"

	
	WSDATA CLIENTE3    AS STRING
	
	WSMETHOD GET DESCRIPTION "Clientes em terceiros" WSSYNTAX "/MAHCLI3" 
 
END WSRESTFUL


/*


ͻ
Programa   GET       Autor  Ednei R. Silva         Data  MAR/2020 
͹
Desc.      Metodo para listar Cliente em Terceiro	    Protheus.     
͹
Uso                                                                   
ͼ


*/
WSMETHOD GET WSRECEIVE CLIENTE3 WSSERVICE MAHCLI3

	Local cQuery	:= ""
	Local cArea		:= ""
	Local aCampo	:= {}
	Local nX		:= 0
	Local nCount	:= 0
	Local nReg		:= 0
	Local nCampo	:= 0
	Local lOk		:= .T.	

	// DWT Luciano - 20231107
	Local cTesInv   := AllTrim( SuperGetMV( "ES_TESINV", .F., "" ) )

	// Define o tipo de retorno do metodo
	::SetContentType("application/json")
	
	aAdd(aCampo, {"B6_CLIFOR", "CODIGO"})	
	aAdd(aCampo, {"B6_LOJA"  , "LOJA"})
    aAdd(aCampo, {"A1_NOME"  , "NOME"})
    aAdd(aCampo, {"B6_LOCAL" , "ARMAZEM"})
    aAdd(aCampo, {"A3_COD"   , "VENDEDOR"})
    aAdd(aCampo, {"A3_NOME"  , "NOME_VENDEDOR"})
	aAdd(aCampo, {"A1_CGC"   , "CNPJ"})
	aAdd(aCampo, {"A1_END"   , "ENDERECO"})
	aAdd(aCampo, {"A1_MUN"   , "MUNICIPIO"})
	nCampo := Len(aCampo)
		
	cQuery := " SELECT "
	aEval( aCampo, { |Z| nX++, cQuery += Z[1] +" "+ Z[2] + IIf( nX < nCampo, ', ', '') } )
	cQuery += " FROM " + RetSQLName("SB6")+ " SB6 " 	
	cQuery += " INNER JOIN  " + RetSQLName("SD2") + " SD2 ON (SD2.D2_DOC=SB6.B6_DOC AND SD2.D2_SERIE=SB6.B6_SERIE AND SD2.D2_CLIENTE=SB6.B6_CLIFOR    AND SD2.D2_LOJA=SB6.B6_LOJA AND SD2.D2_ITEM='01' AND SD2.D_E_L_E_T_<>'*')  "  
	cQuery += " INNER JOIN  " + RetSQLName("SC5") + " SC5 ON (SD2.D2_PEDIDO=SC5.C5_NUM and SD2.D2_FILIAL=SC5.C5_FILIAL AND  SD2.D_E_L_E_T_<>'*') "    
	cQuery += " INNER JOIN  " + RetSQLName("SA1") + " SA1 ON (SC5.C5_CLIENTE=SA1.A1_COD AND SC5.C5_LOJACLI=SA1.A1_LOJA AND SA1.D_E_L_E_T_<>'*') "   
	cQuery += " INNER JOIN  " + RetSQLName("SA3") + " SA3 ON (SA3.A3_COD=SC5.C5_VEND1 AND SA3.D_E_L_E_T_<>'*')    JOIN SB1010 SB1 ON (SB6.B6_PRODUTO=SB1.B1_COD AND  SB1.D_E_L_E_T_<>'*') "    
	cQuery += " INNER JOIN  " + RetSQLName("SF4") + " SF4 ON (SB6.B6_FILIAL=SF4.F4_FILIAL AND SB6.B6_TES=SF4.F4_CODIGO AND SF4.D_E_L_E_T_<>'*') "
	cQuery += " WHERE SB6.D_E_L_E_T_ <> '*'"
	cQuery += " AND SB6.B6_SALDO>0  "
	cQuery += " AND SB6.B6_TIPO = 'E' "  	 	

	// DWT Luciano * 20231023
	If !Empty( cTesInv )
	
		cQuery += " AND SB6.B6_TES  IN ("+ GETMV("ES_TESINV")+") "
	EndIf
	
	cQuery += " AND SB1.B1_GRUPO IN ('2810','2820','2830','2840','2840','2850','2860','2870','2880','2890','2250') "
	cQuery += " GROUP BY   "
	cQuery += " B6_CLIFOR, " 
	cQuery += " B6_LOJA,   " 
	cQuery += " A1_NOME,   "
	cQuery += " B6_LOCAL,  "
	cQuery += " A3_COD,    "
	cQuery += " A3_NOME,   "
	cQuery += " A1_CGC,    "
	cQuery += " A1_END,    "
	cQuery += " A1_MUN     "
	
	cQuery := ChangeQuery( cQuery )
			
	dbUseArea( .T., "TOPCONN", TcGenQry( ,,cQuery), (cArea := GetNextAlias()), .F., .T. )
	
	Count To nReg		

	If nReg == 0
	
		lOk := .F.
		SetRestFault( 1, "Nenhuma Tipo a ser exibida." )

	ELse
	
		dbSelectArea(cArea)
		dbGoTop()

		::SetResponse('{"CLIENTE3": [')

		While (cArea)->( !Eof() )
		
			nCount++

			::SetResponse('{')
			
			nX := 0
			For nX := 1 To nCampo
			
				::SetResponse('"')
				::SetResponse(aCampo[nX][1]) 
				::SetResponse('"')
				::SetResponse(':')
				::SetResponse('"')
				
				If aCampo[nX][1] $ "A1_NOME|A3_NOME|A1_END|A1_MUN"
					::SetResponse( U_NoCharEsp( AllTrim( cValToChar( (cArea)->&(aCampo[nX][2]) ) ) ) )
				Else
					::SetResponse( AllTrim( cValToChar( (cArea)->&(aCampo[nX][2]) ) ) )
				EndIf 
				
				::SetResponse('"')				
				
				If nX < nCampo 
					::SetResponse(',')
				EndIf
							
			Next nX
			
			::SetResponse('}')

			If nCount < nReg
			 	::SetResponse(',')
			EndIf	
	
			(cArea)->( dbSkip() )
	  			
		EndDo
	
		::SetResponse(']}')
		
		(cArea)->( dbCloseArea() )
		
	EndIf
		
Return( lOk )
