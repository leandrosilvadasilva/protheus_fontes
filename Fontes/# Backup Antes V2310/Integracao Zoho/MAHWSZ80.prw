#Include "TOTVS.CH"
#Include "RESTFUL.CH"
/*


Ŀ
Funcao     MAHWS010  Autor  Ednei R. Silva         Data  MAR/2020 
Ĵ
Descricao  Servico Web service para integracao Protheus X Zoho.       
           Cadastro de Vendedores.                                       
Ĵ
Sintaxe                                                               
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno                                                               
Ĵ
 Uso       Especifico MA Hospitalar                                   
Ĵ
                          ULTIMAS ALTERACOES                           
Ĵ
Programador  Data    Motivo da Alteracao                             
Ĵ
															              
ٱ

*/

WSRESTFUL MAHVENDEDOR DESCRIPTION "Cadastro de Vendedor"

	WSDATA A3_COD AS STRING
	WSDATA A3_DTHDE AS STRING 
	WSDATA A3_DTHATE AS STRING 
		
	
	WSMETHOD GET DESCRIPTION "Listar Cadastro de Vendedor" WSSYNTAX "/MAHVENDEDOR" 
 
END WSRESTFUL


/*


ͻ
Programa   GET       Autor  Ednei R. Silva         Data  MAR/2020 
͹
Desc.      Metodo para listar Cadastro de Vendedores Protheus.          
͹
Uso       			                                                   
ͼ


*/
WSMETHOD GET WSRECEIVE A3_COD, A3_DTHDE, A3_DTHATE WSSERVICE MAHVENDEDOR

	Local cQuery	:= ""
	Local cArea		:= ""
	Local aCampo	:= {}
	Local nX		:= 0
	Local nCount	:= 0
	Local nReg		:= 0
	Local nCampo	:= 0
	Local lOk		:= .T.	
	 
	 
	// Define o tipo de retorno do metodo
	::SetContentType("application/json")
		
	aAdd(aCampo, {"A3_COD", "COD"})	
	aAdd(aCampo, {"A3_NOME", "NOME"})
	aAdd(aCampo, {"A3_NREDUZ", "NREDUZ"})
	aAdd(aCampo, {"A3_DDDTEL", "DDDTEL"})
	aAdd(aCampo, {"A3_TEL", "TEL"})
	aAdd(aCampo, {"A3_EMAIL", "EMAIL"})
	aAdd(aCampo, {"A3_MSBLQL", "MSBLQL"})
	aAdd(aCampo, {"A3_ZOHDTH", "ZOHDTH"})
	aAdd(aCampo, {"D_E_L_E_T_", "DELET"})

	nCampo := Len(aCampo)
		
	cQuery := " SELECT "
	aEval( aCampo, { |Z| nX++, cQuery += Z[1] +" "+ Z[2] + IIf( nX < nCampo, ', ', '') } )
	cQuery += "	FROM " + RetSQLName("SA3")
	cQuery += " WHERE A3_FILIAL = '" + xFilial("SA3") + "'"
	cQuery += "   AND A3_MSBLQL <> '1'"
	
	If !Empty( ::A3_COD ) 
		cQuery += "   AND A3_COD = '"+ Upper( ::A3_COD ) +"'"
	EndIf
	
	If !Empty( ::A3_DTHDE ) .Or. !Empty( ::A3_DTHATE )
		cQuery += "  AND A3_ZOHDTH BETWEEN '" + AllTrim(::A3_DTHDE) + "' AND '" + AllTrim(::A3_DTHATE) + "'"
	EndIf
	
	cQuery := ChangeQuery(cQuery)			
	
	dbUseArea( .T., "TOPCONN", TcGenQry( ,,cQuery), (cArea := GetNextAlias()), .F., .T. )
	
	Count To nReg		

	If nReg == 0
	
		lOk := .F.
		SetRestFault( 1, "Nenhum vendedor a ser exibido." )

	ELse
	
		dbSelectArea(cArea)
		dbGoTop()

		::SetResponse('{"VENDEDOR": [')

		While (cArea)->( !Eof() )
		
			nCount++

			::SetResponse('{')
			
			nX := 0
			For nX := 1 To nCampo
			
				::SetResponse('"')
				::SetResponse(aCampo[nX][1]) 
				::SetResponse('"')
				::SetResponse(':')
				::SetResponse('"')
				
				If aCampo[nX][1] $ "A3_NOME|A3_NREDUZ"
					::SetResponse( U_NoCharEsp( AllTrim( cValToChar( (cArea)->&(aCampo[nX][2]) ) ) ) )
				Else
					::SetResponse( AllTrim( cValToChar( (cArea)->&(aCampo[nX][2]) ) ) )
				EndIf 
				
				::SetResponse('"')				
				
				If nX < nCampo 
					::SetResponse(',')
				EndIf
							
			Next nX
			
			::SetResponse('}')

			If nCount < nReg
			 	::SetResponse(',')
			EndIf	
	
			(cArea)->( dbSkip() )
	  			
		EndDo
	
		::SetResponse(']}')
		
		(cArea)->( dbCloseArea() )
		
	EndIf
		
Return( lOk )